apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

buildscript {
    ext {
        buildToolsVersion = "33.0.0"
        minSdkVersion = 21
        compileSdkVersion = 33
        targetSdkVersion = 33
        kotlinVersion = "1.8.0"
        reactNativeVersion = "0.72.0"
    }
    if (project == rootProject) {
        repositories {
            maven { url "https://maven.google.com" }
            mavenCentral()
            google()
        }

        dependencies {
            classpath("com.android.tools.build:gradle:7.3.1")
            classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        }
    }
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def DEFAULT_COMPILE_SDK_VERSION = 31
def DEFAULT_BUILD_TOOLS_VERSION = "31.0.0"
def DEFAULT_MIN_SDK_VERSION = 16
def DEFAULT_TARGET_SDK_VERSION = 31

def isNewArchitectureEnabled() {
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

android {
    def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')[0].toInteger()
    if (agpVersion >= 7) {
        namespace 'com.proyecto26.inappbrowser'
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion safeExtGet('buildToolsVersion', DEFAULT_BUILD_TOOLS_VERSION)
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
      main {
        if (isNewArchitectureEnabled()) {
          java.srcDirs += ["src/newarch"]
        } else {
          java.srcDirs += ["src/oldarch"]
        }
      }
    }
}

repositories {
    mavenCentral()
    google()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../node_modules/react-native/android"
    }
    maven {
        // Android JSC is installed from npm
        url("$rootDir/../node_modules/jsc-android/dist")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    
    // React Native
    implementation "com.facebook.react:react-native:${safeExtGet('reactNativeVersion', '+')}"
    
    // EventBus
    def eventbusVersion = safeExtGet('eventbus', safeExtGet('eventbus', '3.3.1'))
    implementation "org.greenrobot:eventbus:$eventbusVersion"

    // AndroidX Browser
    def androidXAnnotation = safeExtGet('androidXAnnotation', '1.3.0')
    def androidXBrowser = safeExtGet('androidXBrowser', '1.9.0') // v1.9.0.alpha has ephemeralSession support
    def androidXCore = safeExtGet('androidXCore', '1.9.0')
    implementation "androidx.annotation:annotation:$androidXAnnotation"
    implementation "androidx.browser:browser:$androidXBrowser"
    implementation "androidx.core:core-ktx:$androidXCore"
    
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}
  
